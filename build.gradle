buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'com.github.rodm', name: 'gradle-teamcity-plugin', version: '1.0'
    }
}

if (PluginVersion == null || "" == PluginVersion) {
    version = TeamCityVersion
} else {
    version = "$PluginVersion"
}
if (PluginVersion == null || "" == PluginVersion) {
    PluginVersion = "0.1-SNAPSHOT"
}

ext {
    teamcityDir = "$rootDir/servers/TeamCity-${TeamCityVersion}"
    teamcityDataDir = "$rootDir/data/" + (
            (TeamCityVersion ==~ /(\d+\.\d+).*/) ?
                    ((TeamCityVersion =~ /(\d+\.\d+).*/)[0][1])
                    : TeamCityVersion
    )
    teamcityJavaHome = System.properties['java.home']
    teamcityGeneratedPoms = "$buildDir/generated-poms"
    teamcityInstall = project.hasProperty("teamcityInstall") ? project.getProperty("teamcityInstall") : false
}

allprojects {
    group = 'org.jetbrains.teamcity.output_parser'
    version = PluginVersion

    configurations {
        provided
    }

    if (!teamcityInstall) {
        project.tasks.findByName("downloadTeamcity91")*.setEnabled(false)
    }
}

subprojects {
    apply plugin: 'java'
    dependencies {
        testCompile("org.jetbrains.teamcity:tests-support:${TeamCityVersion}")
    }
    sourceCompatibility = "1.6"
    targetCompatibility = "1.6"

    test {
        useTestNG()
    }

    apply plugin: 'maven-publish'
    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
    }

    task testsSourcesJar(type: Jar) {
        from sourceSets.test.allJava
    }

    task testsJar(type: Jar) {
        from sourceSets.test.output
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                version project.version
                from components.java
                artifact sourcesJar {
                    classifier "sources"
                }
                artifact testsSourcesJar {
                    classifier "test-sources"
                }
                artifact testsJar {
                    classifier "test"
                }
            }
        }

        repositories {
            maven {
                url "${rootProject.buildDir}/repo"
            }
        }
    }
}

apply plugin: 'com.github.rodm.teamcity-server'
apply plugin: 'idea'
//apply from: 'export-teamcity-to-local-maven.gradle'

dependencies {
    agent project(path: ':text-translators-agent', configuration: 'plugin')
}

teamcity {
    version = TeamCityVersion

    server {
        descriptor {
            name = "process-output-parsers"
            displayName = 'Framework for process output parsers'
            version = project.version
            vendorName = 'JetBrains, s.r.o.'
            vendorUrl = 'http://www.jetbrains.com'
            useSeparateClassloader = true
        }

        environments {
            teamcity100 {
                homeDir = file(teamcityDir)
                dataDir = file(teamcityDataDir)
                javaHome = file(teamcityJavaHome)

                serverOptions '-Xdebug'
                serverOptions '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5500'
                serverOptions '-Dteamcity.oauthConnections.enabled=true'
            }
        }
    }
}


idea {
    module {
        downloadJavadoc = false
        downloadSources = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}
